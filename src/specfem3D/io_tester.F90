module io_bandwidth
    use mpi, only: MPI_Wtime

    implicit none
    double precision :: start_time, end_time, time_delta
    integer :: bytes_written

  contains

    subroutine initialize_bytes_written()
      bytes_written = 0
      time_delta = 0.0
    end subroutine initialize_bytes_written

    subroutine start_timer()
        start_time = MPI_Wtime()
    end subroutine start_timer

    subroutine stop_timer()
        end_time = MPI_Wtime()
        time_delta = time_delta + (end_time - start_time)
    end subroutine stop_timer

    subroutine set_bytes_written(bytes)
      implicit none
      integer, intent(in) :: bytes
      bytes_written = bytes
    end subroutine set_bytes_written

    subroutine set_bytes_written_from_array(element_size, num_elements)
        implicit none
        integer, intent(in):: element_size, num_elements

        ! element_size is the size of each element in bits (as storage_size returns in bits)

        ! Calculate the total bytes written and add to the existing value
        bytes_written = bytes_written + element_size * num_elements / 8 ! Convert bits to bytes

      end subroutine set_bytes_written_from_array


    subroutine calculate_bandwidth_this_proc()
      implicit none
      double precision :: elapsed_time, bandwidth

      elapsed_time = time_delta
      if (elapsed_time > 0.0) then
        bandwidth = bytes_written / (elapsed_time * 1.0e6)  ! Bandwidth in MB/s
        print *, 'I/O Bandwidth: ', bandwidth, ' MB/s'
      else
        print *, 'Elapsed time is zero, cannot calculate bandwidth'
      endif
    end subroutine calculate_bandwidth_this_proc

    ! calculate total bandwidth across all processes
    subroutine calculate_bandwidth_all_procs()
      use specfem_par
      use shared_parameters
      implicit none
      integer :: i, total_bytes, unit_number, ierr
      double precision :: elapsed_time, max_elapsed_time, &
              bandwidth, total_bandwidth
      character(len=20) :: filename
      character(len=10) :: mygroup_str
      logical :: file_exists

      ! Convert mygroup to a character string
      write(mygroup_str, '(I0)') mygroup

      ! Create the filename
      filename = 'io_band_' // trim(adjustl(mygroup_str)) // '.txt'
      unit_number = 1000010010 + mygroup

      ! Check if the file exists
      inquire(file=filename, exist=file_exists)

      ! Open the file for writing
      if (myrank == 0) then
        ! create the file if it does not exist
        if (file_exists) then
          open(unit=unit_number, file=filename, status='old', action='write', position='append', iostat=ierr)
        else
          open(unit=unit_number, file=filename, status='new', action='write', iostat=ierr)
        end if
        if (ierr /= 0) then
            print*, 'Error opening file: ', filename
            stop
        end if
        write(unit_number, *) '-------------------------------------------------------------------------------'
        close(unit_number)
      end if

      call synchronize_all()

      elapsed_time = time_delta
      if (elapsed_time > 0.0) then
          bandwidth = bytes_written / (elapsed_time * 1.0e6)  ! Bandwidth in MB/s

          ! calculate total bytes written across all processes
          call sum_all_all_i(bytes_written, total_bytes)
          ! get the maximum elapsed time across all processes
          call max_all_dp(elapsed_time, max_elapsed_time)

          ! calculate total bandwidth
          total_bandwidth = total_bytes / (max_elapsed_time * 1.0e6) ! Bandwidth in MB/s

          ! Each process writes to the file sequentially
          do i = 0, NPROCTOT_VAL-1
              if (myrank == i) then
                  open(unit=unit_number, file=filename, status='old', action='write', position='append', iostat=ierr)
                  if (ierr /= 0) then
                      print*, 'Error opening file: ', filename
                      stop
                  end if
                  write(unit_number, '(A, I0, A, I0, A, I0, A, F12.6, A, F12.6, A, F12.6)') &
                      'mygroup: ', mygroup, ', myrank: ', myrank, ', bytes_written: ', bytes_written, &
                      ', elapsed_time (s): ', elapsed_time, ', bandwidth: ', bandwidth, ' MB/s'
                  close(unit_number)
              end if
              call synchronize_all()
          end do

          ! Only the root process writes the total bandwidth
          if (myrank == 0) then
              open(unit=unit_number, file=filename, status='old', action='write', position='append', iostat=ierr)
              if (ierr /= 0) then
                  print*, 'Error opening file: ', filename
                  stop
              end if
              write(unit_number, '(A, I0, A, I0, A, F12.6, A, F12.6, A, F12.6)') &
                  'mygroup: ', mygroup, ', total_bytes_written: ', total_bytes, ', max_elapsed_time (s): ', &
                  max_elapsed_time, ', total_bandwidth: ', total_bandwidth, ' MB/s'
              close(unit_number)
          end if
      end if
    end subroutine calculate_bandwidth_all_procs

  end module io_bandwidth